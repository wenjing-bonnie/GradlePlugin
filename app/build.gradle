plugins {
    id 'com.android.application'
    id 'kotlin-android'
    //验证Android 12 的适配
    id 'com.mob.sdk'
    //对manifest进行exported适配及version管理
    id 'com.wj.manifest'
    //gradle plugin高级进阶
    id 'com.wj.seniorapplication'
}

/**com.wj.manifest中配置的扩展属性*/
manifestKotlin {
    versionManager file("version.xml")
    compileSdkVersion 31
    buildToolVersion "30.0.3"
    defaultConfig {
        applicationId "com.wj.gradle.plugin"
        minSdkVersion 23
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles file("proguard-rules.pro")
        }
        dev {
            minifyEnabled true
            proguardFiles file("proguard-rules.pro")
        }
    }
}

//TODO 暂时不知道为什么下面这个情况
// 若进行配置自定义的configurations，必须configurations后面紧挨着dependencies
// 否则会抛出编译异常，暂时不知道原因
// 同时还要配置android{}中的productFlavors{},否则会抛出"Cannot change attributes of dependency configuration ':app:debugCompile' after it has been resolved"
configurations {
    codec {
        visible false
        canBeConsumed false
        canBeResolved true
        attributes {
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage,Usage.JAVA_RUNTIME))
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    codec "commons-codec:commons-codec:1.10"

}


/**com.wj.seniorapplication配置的扩展属性*/
seniorApplicationKotlin {
    incremental {
        inputFile file("inputs/1.txt")
        inputFiles files("inputs")
        inputDir file("inputs")
        outputFile file("outputs/output.txt")
    }
    lazyExtensionProperty = file("inputs/1.txt")
    md5CodecClasspath.from(configurations.codec.files)
}


//第三种：监听Project配置阶段
this.beforeEvaluate {

}

this.afterEvaluate {

}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.wj.gradle.plugin"
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    flavorDimensions "channal"
    productFlavors {
        huawei {
            dimension = "channal"
        }
    }
}

